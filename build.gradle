buildscript {
    ext {
        cleanArchitectureVersion = '3.24.0'
        springBootVersion = '3.5.4'
        sonarVersion = '6.2.0.5505'
        jacocoVersion = '0.8.13'
        pitestVersion = '1.19.0-rc.1'
        lombokVersion = '1.18.38'
        mapstructVersion = '1.6.3'
    }
}

plugins {
    id 'co.com.bancolombia.cleanArchitecture' version "${cleanArchitectureVersion}"
    id 'org.springframework.boot' version "${springBootVersion}" apply false
    id 'io.spring.dependency-management' version '1.1.6' apply false
    id 'info.solidsoft.pitest' version "${pitestVersion}" apply false
    id 'org.sonarqube' version "${sonarVersion}"
    id 'jacoco'
}

sonar {
    def modules = subprojects.projectDir.collect { "${it.toString().replace(project.projectDir.toString() + "/", "")}" }
    properties {
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.modules", "${modules.join(',')}"
        property "sonar.sources", "src,deployment,settings.gradle,main.gradle,build.gradle,${modules.collect { "${it}/build.gradle" }.join(',')}"
        property "sonar.exclusions","**/MainApplication.java"
        property "sonar.tests", "src/test"
        property "sonar.java.binaries", "**/build/classes/java/main"
        property "sonar.junit.reportsPath", "**/build/test-results/test"
        property "sonar.java.coveragePlugin", "jacoco"
        property "sonar.coverage.jacoco.xmlReportPaths", "build/reports/jacocoMergedReport/jacocoMergedReport.xml"
        property "sonar.pitest.reportPaths", "build/reports/pitest/mutations.xml"
        property "sonar.externalIssuesReportPaths", "build/issues.json"
    }
}

apply from: './main.gradle'

// ===== A N A D I D O  =====
// Alineación global de dependencias con el BOM de Spring Boot y
// forzar una sola versión de Spring Security compatible con Boot 3.5.4.
ext {
    springSecurityAlignedVersion = '6.4.8' // la que trae Boot 3.5.4
}

subprojects {
    apply plugin: 'io.spring.dependency-management'

    dependencyManagement {
        imports {
            // Usa el BOM de Boot para versionar TODA la familia Spring
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
        }
    }

    configurations.configureEach {
        resolutionStrategy.eachDependency { details ->
            if (details.requested.group == 'org.springframework.security') {
                details.useVersion springSecurityAlignedVersion
                details.because 'Alinear Spring Security con Boot 3.5.4 y evitar mezcla 6.5.x/6.2.x'
            }
        }
    }
}

